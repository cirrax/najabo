#!/usr/bin/perl

# najabo - Nagios Jabber Bot
#
# Authors:
#   Thomas Liske <thomas@fiasko-nw.net>
#
# Copyright Holder:
#   2011 (C) Thomas Liske <thomas@fiasko-nw.net>
#
# License:
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this package; if not, write to the Free Software
#   Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301 USA
#

use Config;
use File::Basename;
use Getopt::Std;
use Linux::Inotify2;
use Log::Dispatch;
use Log::Dispatch::Screen;
use Log::Dispatch::Syslog;
use Log::Dispatch::File;
use POSIX qw(strftime);
use Proc::Daemon;
use Proc::PID::File;
use Net::XMPP;
use Nagios::StatusLog;

use strict;
use warnings;

use constant {
    NAJABOD_VERSION => '0.1',

    XML_IQ_VERSION =>
"<query xmlns='jabber:iq:version'>
    <name>najabod</name>
    <version>0.1</version>
    <os>nagios %NAGVER%($Config{osname})</os>
</query>",

    XML_IQ_NOVCARD =>
"<vCard xmlns='vcard-temp'/>",

    XML_IQ_DISCO =>
"<query xmlns='http://jabber.org/protocol/disco#info'>
    <feature var='jabber:iq:last'/>
    <feature var='jabber:iq:version'/>
    <feature var='urn:xmpp:time'/>
    <feature var='vcard-temp'/>
</query>",
};

my $logfmt = sub { my %p = @_; chomp($p{'message'}); return ("[$p{level}] $p{message}\n"); };
my $logffile = sub { my %p = @_;  return (scalar localtime())." [$$] $p{message}"; };
my $logger = Log::Dispatch->new(callbacks => $logfmt);

END {
    $logger->notice('terminated') if(defined($logger));
}

$logger->add(
    Log::Dispatch::Screen->new(
	name => 'console',
	min_level => 'notice',
    )
);

sub mydie($) {
    $logger->critical(shift);
    exit 1;
}

our $opt_c = '/etc/najabo/najabo.conf';
our $opt_f;
getopts('fc:');
mydie("Could not read config file '$opt_c'.") unless(-r $opt_c);
$logger->info("config file: '$opt_c'");

my %xmpp_conf;
my %auth_conf;
my %nagios_conf;
my %najabo_conf;
eval `cat "$opt_c"`;

# enable log to file
if(exists($najabo_conf{'logfile'})) {
    $logger->add(
	Log::Dispatch::File->new(
	    name => 'logfile',
	    min_level => 'info',
	    filename => $najabo_conf{'logfile'},
	    close_after_write => '1',
	    callbacks => $logffile,
	)
    );
}
# fallback to syslog
else {
    $logger->add(
	Log::Dispatch::Syslog->new(
	    name => 'syslog',
	    min_level => 'info',
	    ident => 'najabod',
	    logopt => 'pid',
	    facility => 'daemon',
	)
    );
}

$logger->info('najabod '.NAJABOD_VERSION);

my $conn;
my $last;


# send vCard IQ
sub SendVCard(%) {
    my $iq = new Net::XMPP::IQ();
    $iq->SetIQ(@_);

    if (exists($najabo_conf{'vcard'}) &&
        defined($najabo_conf{'vcard'}) &&
        (-r $najabo_conf{'vcard'}) &&
        open(FILEREAD, '<', $najabo_conf{'vcard'})) {

        $iq->InsertRawXML(<FILEREAD>);
        close(FILEREAD);
    }
    else {
        $iq->InsertRawXML(XML_IQ_NOVCARD);
    }

    $conn->Send($iq);
}


# setup Nagios::StatusLog stuff
$logger->info("nagios status log: $nagios_conf{Filename}");
$logger->info("nagios version: $nagios_conf{Version}");

my $log;
eval { $log = Nagios::StatusLog->new(%nagios_conf); };
mydie "Failed to initialize Nagios::StatusLog: $@" if($@);

unless($opt_f) {
    # daemonize
    eval { Proc::Daemon::Init() };
    mydie "Unable to fork daemon: $@" if($@);

    # check if we are alone
    eval {
	die "Daemon is already running or PID file is stale!" if(Proc::PID::File->running(verify => 1, dir => $najabo_conf{'rundir'}));
    };
    mydie "Failed to setup PID file: $@" if($@);

    # detach log to console
    $logger->notice('Could not remove console dispatcher!') unless($logger->remove('console'));
}

# setup inotify
my $inotify = new Linux::Inotify2 or mydie "Unable to create new inotify object: $!";
$inotify->blocking(0);

# add inotify watch
my $watch = $inotify->watch(dirname($nagios_conf{'Filename'}), IN_MOVED_TO, sub {
    my $e = shift;
    ParseStatus() if($e->IN_MOVED_TO && ($e->fullname eq $nagios_conf{'Filename'}));
}) or mydie "Could not setup inotify watch: $!";

while(1) {
    $conn = new Net::XMPP::Client;

    # register callbacks
    $conn->SetCallBacks(message=>\&InMessage, iq=>\&InIQ);

    # try to connect
    unless($conn->Connect(%xmpp_conf)) {
        $logger->warning("Could not connect to XMPP server ($!), sleeping 30 seconds!");
        sleep(30);
        next;
    }
    $logger->info('XMPP server connected');

    # try to auth
    my @result = $conn->AuthSend(%auth_conf);
    unless($result[0] eq 'ok') {
        $logger->warning("Auth failed ($result[1]), sleeping 60 seconds!");
        sleep(60);
        next;
    }
    $logger->info('XMPP auth ok');

    $conn->PresenceSend(
	status => 'Initializing...',
	show => 'dnd',
    );
    SendVCard(type => 'set');

    # wait for XMPP messages and nagios status updates
    while(defined($conn->Process(5))) {
	$inotify->poll;
    }

    $logger->warning('Connection broken, sleeping 30 seconds!');
    sleep(30);
}

sub SortStates {
    my %o = (
        'DOWN' => 0,
        'CRITICAL' => 1,
        'WARNING' => 2,
        'UNREACHABLE' => 3,
        'UNKNOWN' => 4,
        'OK' => 5,
    );

    return $a cmp $b unless(exists($o{$a}) && exists($o{$b}));
    return $o{$a} <=> $o{$b};
 }

sub ParseStatus {
    $log->update();

    my %states;
    foreach my $host ($log->list_hosts) {
	my $obj = $log->host($host);

	if(scalar grep {/^$obj->status$/i} qw(OK WARNING CRITICAL)) {
	    foreach my $service ($log->list_services_on_host($host)) {
		my $srv = $log->service($host, $service);

		push(@{$states{$srv->status}}, $obj->host_name."/$service")
		    if(defined($srv->status));
	    }
	}
	else {
	    push(@{$states{$obj->status}}, $obj->host_name)
	}
    }

    my @states;
    my @details;
    foreach my $status (sort SortStates keys %states) {
	my $t = $status;
	$t =~ s/^(.{4}).+/$1/;

	push(@states, ($#{$states{$status}} + 1).' '.$t);
	push(@details, $status) unless(scalar grep {/^$status$/i} @{$najabo_conf{'filter_states'}});
    }

    my $status = join(', ', @states);
    $status .= "\n" unless($#details == -1);
    foreach my $detail (@details) {
	$status .= "\n$detail:\n ".join("\n ", sort @{$states{$detail}})."\n";
    }

    # map nagios status
    my $show = 'online';
    if(exists($states{'DOWN'}) || exists($states{'CRITICAL'})) {
	$show = 'xa';
    }
    elsif(exists($states{'WARNING'})) {
	$show = 'away';
    }

    $conn->PresenceSend(
	status => $status,
	show => $show,
    );
}

# we've got a message
sub InMessage {
#    my $sid = shift;
#    my $message = shift;
#    my $to = $message->GetFrom();
#
#    if ($to =~ /\@/) {
#        if (my $m = $message->GetBody()) {
#            my $response = $eliza->transform($m);
#
#            sleep 0.5+rand;
#
#            $conn->Send($message->Reply(body=>$response));
#        }
#    }
}

# somebody query's an IQ
sub InIQ {
    my $sid = shift;
    my $iq = shift;

    my $from = $iq->GetFrom();
    my $to = $iq->GetTo();
    my $id = $iq->GetID();
    my $type = $iq->GetType();
    my $query = $iq->GetQuery();

    if($type eq 'get') {
        my $xmlns = $query->GetXMLNS();

        if ($xmlns eq 'jabber:iq:version') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');

            my $ver = XML_IQ_VERSION;
            my $i;
            if(defined($i = $log->info) && defined($i->version)) {
                $ver =~ s/%NAGVER%/${$i}{version} /;
            }
            else {
                $ver =~ s/%NAGVER%//;
            }

            $iq->InsertRawXML($ver);
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'jabber:iq:last') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');

            my $seconds = '';
            my $i;
            if(defined($i = $log->info) && defined($i->created)) {
                $seconds = "seconds='".(time() - $i->created)."'";
            }

            $iq->InsertRawXML("<query xmlns='jabber:iq:last' $seconds/>");
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'urn:xmpp:time') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');

            my $tzo = strftime('%z', localtime);
            $tzo =~ s/(.+)(..)/$1:$2/;
            my $utc = strftime('%FT%TZ', gmtime);
            $iq->InsertRawXML(
"<time xmlns='urn:xmpp:time'>
    <tzo>$tzo</tzo>
    <utc>$utc</utc>
</time>"
            );
            $conn->Send($iq);
        }
        elsif ($xmlns eq 'vcard-temp') {
            SendVCard(
                id => $id,
                type => 'result',
                to => $from,
                from => $to,
            );
        }
        elsif ($xmlns eq 'http://jabber.org/protocol/disco#info') {
            my $iq = new Net::XMPP::IQ();
            $iq->SetIQ(to => $from, from => $to, id => $id, type => 'result');
            $iq->InsertRawXML(XML_IQ_DISCO);
            $conn->Send($iq);
        }
        else {
            $logger->warning("Unhandled IQ: $xmlns");
        }
    }
}
